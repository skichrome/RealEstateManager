apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

def apiProperties = new Properties()
apiProperties.load(new FileInputStream(rootProject.file("apikey.properties")))

android {
    signingConfigs {
        release {
            def keyStorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keyStorePropertiesFile))

            storeFile file(keystoreProperties['keystore_file'])
            storePassword keystoreProperties['keystore_file_pwd']
            keyAlias keystoreProperties['keystore_alias']
            keyPassword keystoreProperties['keystore_pwd']
        }
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.skichrome.realestatemanager"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "GEOCODING_API_KEY", apiProperties['google_geocoding_api'])
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("string", "MAP_API_KEY", apiProperties['android_google_map_sdk_api_release'])
        }
        debug {
            resValue("string", "MAP_API_KEY", apiProperties['android_google_map_sdk_api_debug'])
        }
    }
    compileOptions {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildToolsVersion = '29.0.2'

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.1.0'

    // Tests
    testImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test:rules:1.2.0'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // RecyclerView
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta05'

    // NavDrawer
    implementation 'androidx.drawerlayout:drawerlayout:1.0.0'

    // Design library
    implementation 'com.google.android.material:material:1.1.0-beta01'

    //Preferences Screen
    implementation 'androidx.preference:preference:1.1.0'

    // Easy Permissions
    implementation 'pub.devrel:easypermissions:3.0.0'

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:${navigation_component_version}"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_component_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // Facebook debugging tool
    implementation 'com.facebook.stetho:stetho:1.5.1'

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'

    // Play services : map and location
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'
}